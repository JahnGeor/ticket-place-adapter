plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.8.22'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.runtime' version "1.12.5"
    //id 'io.ktor.plugin' version '2.3.12'
    id 'org.jetbrains.kotlin.plugin.serialization' version '2.0.0'
}

group 'ru.kidesoft.ticketplace'
version '2.0.0'

//shadowJar {
//    archiveFileName.set("Ticket Place-${project.version}.jar")
//}

run {
    doFirst {
        mkdir("$buildDir/workingDir")
        workingDir = './build/workingDir'

        copy {
            from("$projectDir/driver/nt-x64-msvc2015") into "$workingDir/driver/kkt/atol"
        }
    }

    jvmArgs += ['-Dlog4j2.configurationFile=classpath:log4j2.xml']
    jvmArgs += ['-Dfile.encoding=utf-8']

}

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.10.0'
}


tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

application {
    mainClass = 'ru.kidesoft.ticketplace.adapter.Main'
}

kotlin {
    jvmToolchain(17)
}

javafx {
    version = '17.0.6'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation files("$projectDir/libs/libfptr10.jar")

    implementation('org.controlsfx:controlsfx:11.1.2')
    implementation('org.kordamp.ikonli:ikonli-javafx:12.3.1')
    implementation "org.jetbrains.kotlin:kotlin-reflect:2.0.0"
    implementation 'io.github.mkpaz:atlantafx-base:2.0.1'
    implementation 'net.datafaker:datafaker:2.3.0'
    implementation 'org.kordamp.ikonli:ikonli-fluentui-pack:12.3.1'
    implementation 'org.kordamp.ikonli:ikonli-materialdesign2-pack:12.3.1'

    implementation 'org.apache.logging.log4j:log4j-api:2.23.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.23.1'

    implementation 'com.h2database:h2:2.2.224'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.6.3'
    implementation 'com.google.code.gson:gson:2.7'
    implementation "org.flywaydb:flyway-core:10.15.2"

    implementation 'org.jetbrains.exposed:exposed-core:0.52.0'
    implementation 'org.jetbrains.exposed:exposed-jdbc:0.52.0'
    implementation 'org.jetbrains.exposed:exposed-dao:0.52.0'
    implementation("org.jetbrains.exposed:exposed-java-time:0.52.0")


    implementation "io.ktor:ktor-client-core:2.3.12"
    implementation 'io.ktor:ktor-client-cio-jvm:2.3.12'
    implementation "io.ktor:ktor-serialization-gson:2.3.12"
    implementation 'io.ktor:ktor-client-content-negotiation:2.3.12'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'


    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}



runtime {
    options = ['--strip-debug', '--no-header-files', '--no-man-pages']

    modules = [
            'javafx.base',
            'javafx.graphics',
            'javafx.controls',
            'javafx.fxml',
            'java.scripting',
            'java.xml',
            'java.desktop',
            'java.logging',
            'javafx.media',
            'java.sql',
            'java.management',
            'jdk.unsupported',
            'jdk.jfr',
            'jdk.crypto.ec'

    ]

    launcher {
        noConsole = true
    }

    jpackage {

        //mainJar = "$buildDir/libs/Ticket Place-2.0.0.jar"
        jvmArgs += ['-Dfile.encoding=utf-8']
        imageOptions += ['--icon', 'src/main/resources/ru/kidesoft/ticketplace/adapter/assets/icon.ico']
        installerOptions += ['--resource-dir', 'src/main/resources']
        installerOptions += ['--vendor', 'Kidesoft']
        installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']


    }


}

tasks.jpackage.doFirst {
    copy {
        from("$projectDir/driver/nt-x64-msvc2015") into "$buildDir/jpackage/$project.name/driver/kkt/atol"
    }
}

//jlink {
//  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
//  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
//  launcher {
//    name = 'app'
//  }
//}
//
//jlinkZip {
//  group = 'distribution'
//}